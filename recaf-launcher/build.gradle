buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.3.0beta1'
    }
}

// https://docs.gradle.org/current/userguide/toolchains.html
// gradlew -q javaToolchains - see the list of detected toolchains.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    implementation common_io
    implementation directories
    implementation gson
    implementation slf4j
    implementation logback_classic
    implementation picocli
}

jar {
    manifest.attributes('Main-Class': 'dev.xdark.recaf.Launcher')
}

task fatJar(type: Jar) {
    with jar
    archiveBaseName.set(archiveBaseName.get() + '-fat')
    from {
        configurations.runtimeClasspath.collect { zipTree(it) }
    }
    manifest.attributes('Main-Class': 'dev.xdark.recaf.Launcher')
}

task shrinkJar(type: proguard.gradle.ProGuardTask) {
    dontwarn() // Should we really enable that?
    if (!JavaVersion.current().java9Compatible) {
        libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    }
    libraryjars files(configurations.runtimeClasspath.collect())
    dontobfuscate()
    optimizationpasses 9
    overloadaggressively()
    mergeinterfacesaggressively()
    allowaccessmodification()
    keep "public final class dev.xdark.recaf.Launcher {public static void main(java.lang.String[]);}"
    target "1.8"
    dependsOn fatJar
    injars fatJar.archivePath
    outjars "${fatJar.destinationDirectory.get()}/recaf-launcher-shrink-${project.version}.jar"
    keep "public class ch.** { *; }"
    keep "public class com.google.gson.Gson { <fields>; }"
}
