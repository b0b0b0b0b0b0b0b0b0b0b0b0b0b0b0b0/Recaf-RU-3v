plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'application'
}

mainClassName = "me.coley.recaf.Main"

configurations {
    common
    implementation.extendsFrom common
    // javafx
    testImplementation.extendsFrom compileOnly
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation project(':recaf-core')
    implementation project(':recaf-plugin')
    implementation docking
    implementation fuzzy
    implementation richtextfx
    implementation 'com.github.Col-E:fxgraph:0.0.4'
    implementation pe
    implementation jelf

    // We compile against JFX but do not bundle. The launcher will download the platform specific implementation for end users.
    def javafxPlatform = getJavaFxPlatform()
    def javafxVersion = '19.0.2'
    compileOnly group: 'org.openjfx', name: 'javafx-base', version: javafxVersion, classifier: javafxPlatform
    compileOnly group: 'org.openjfx', name: 'javafx-graphics', version: javafxVersion, classifier: javafxPlatform
    compileOnly group: 'org.openjfx', name: 'javafx-controls', version: javafxVersion, classifier: javafxPlatform
    compileOnly group: 'org.openjfx', name: 'javafx-media', version: javafxVersion, classifier: javafxPlatform

    // Testing
    testImplementation project(path: ':recaf-core', configuration: 'testSourceOutput')
    testImplementation junit_api
    testImplementation junit_params
    testRuntimeOnly junit_engine
}

shadowJar {
    // Configured to match legacy release naming conventions.
    // This prevents major breaking of prior automated systems that depend on
    // the full release of the jar fitting a given pattern.
    archiveBaseName.set('recaf-ru')
    archiveClassifier.set('Java11-or-higher')
    archiveVersion.set(project.version)
    // We do not really want to minimize the output (auto-remove unused code)
    // but the shadow plugin provides no other way of filtering output.
    // This works, but I'm very confused by it.
    minimize {
        // EXCLUDE contents from dependencies, prevents adding to final shadowJar
        exclude(dependency("ch.qos.logback:logback-classic:.*") )
        dependencies {
            exclude "com/ibm/icu/impl/data/icudt70b/**"
            exclude "com/ibm/icu/impl/data/icudt69b/**"
        }
    }
}

private static def getJavaFxPlatform() {
    def os = System.getProperty('os.name').toLowerCase(Locale.ENGLISH)
    if (os.contains('win')) {
        return 'win'
    }
    if (os.contains('nix') || os.contains('nux')) {
        return 'linux'
    }
    if (os.contains('osx') || os.contains('mac')) {
        return 'mac'
    }
    assert false: "unknown os: $os"
}

jar {
    manifest.attributes('Main-Class': mainClassName)
}
